plugins {
    id "fabric-loom"
    id "maven-publish"
    id "com.matthewprenger.cursegradle"
    id "com.modrinth.minotaur"
    id "org.jetbrains.changelog"
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+${rootProject.minecraft_version}"
group = project.maven_group

var compatibleWith = []

var split = rootProject.minecraft_version.split('\\.')
if (split.length == 3 && !rootProject.minecraft_version.contains("-")) {
    var base = split[0] + "." + split[1]
    compatibleWith.add(base)
    for (int i = Integer.parseInt(split[2]); i > 0; i--)
        compatibleWith.add(base + "." + i)
} else compatibleWith.add(rootProject.minecraft_version)

repositories {
    maven {
        name = "stashymane's repo"
        url = "https://repo.stashy.dev"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
    mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
}

loom {
    accessWidenerPath = file("src/main/resources/soundcategories.accesswidener")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = Integer.parseInt(targetCompatibility)
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
        maven {
            url = "https://repo.stashy.dev"
            credentials {
                username = project.hasProperty("publish_username") ? project.publish_username : ""
                password = project.hasProperty("publish_password") ? project.publish_password : ""
            }
        }
    }
}

changelog {
    version = project.mod_version
}

curseforge {
    apiKey = System.getenv("CURSE_API_KEY") ?: ""
    project {
        id = "557374"
        changelog = project.changelog.get(project.mod_version).toHTML()
        changelogType = "html"
        releaseType = "release"
        for (String ver : compatibleWith)
            addGameVersion ver

        mainArtifact(remapJar) {
            displayName = "$project.mod_version for $rootProject.minecraft_version"
            relations {
                requiredDependency("fabric-api")
            }
        }
    }

    options {
        forgeGradleIntegration = false
        detectNewerJava = true
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN") ?: ""
    changelog = project.changelog.get(project.mod_version).toText()
    projectId = "GROGt4v1"
    versionNumber = version
    versionName = "$project.mod_version for $rootProject.minecraft_version"
    uploadFile = remapJar
    gameVersions = compatibleWith
    loaders = ["fabric"]
}

task publishMod() {
    dependsOn(tasks.modrinth)
    dependsOn(tasks.curseforge)
}
